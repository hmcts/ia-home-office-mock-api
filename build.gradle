plugins {
  id 'application'
  id 'checkstyle'
  id 'pmd'
  id 'jacoco'
  id 'io.spring.dependency-management' version '1.0.9.RELEASE'
  id 'org.springframework.boot' version '2.7.18'
  id 'org.owasp.dependencycheck' version '10.0.3'
  id 'com.github.ben-manes.versions' version '0.28.0'
  id 'org.sonarqube' version '4.3.0.3225'
  id 'maven-publish'
  id "org.openapi.generator" version "4.3.0"
}

apply plugin: 'java'
apply plugin: 'org.openapi.generator'

group = 'uk.gov.hmcts.reform'
version = '0.0.1'

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(17)
  }
}

sourceSets {
  functionalTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/functionalTest/java')
    }
    resources.srcDir file('src/functionalTest/resources')
  }

  integrationTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/integrationTest/java')
    }
    resources.srcDir file('src/integrationTest/resources')
  }

  smokeTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/smokeTest/java')
    }
    resources.srcDir file('src/smokeTest/resources')
  }
}

def apiGenerateBuildDir = "$buildDir/apidocs".toString()

openApiGenerate {
  generatorName = "spring"
  inputSpec = "$projectDir/appealsAPIv1d.yaml".toString()
  outputDir = apiGenerateBuildDir
  apiPackage = "uk.gov.hmcts.reform.iahomeofficemockapi.generated.infrastructure.api"
  invokerPackage = "uk.gov.hmcts.reform.iahomeofficemockapi.generated.infrastructure.api.invoker"
  modelPackage = "uk.gov.hmcts.reform.iahomeofficemockapi.generated.domain.entities"
  configOptions = [
          dateLibrary: "java8",
          dateLibrary: "java8",
          serializationLibrary: "jackson"
  ]
}

task copyApiDocs(type: Copy) {

  def srcPath = "$apiGenerateBuildDir/src/main/java"
  def targetPath = "$rootDir/src/main/java"

  // copy generated classes
  from(srcPath)
  into(targetPath)
}

tasks.withType(Copy) {
  dependsOn tasks.openApiGenerate
}


tasks.withType(JavaCompile) {
  options.compilerArgs << "-Xlint:unchecked" << "-Werror"
  options.encoding = 'UTF-8'
}

tasks.withType(Test) {
  useJUnitPlatform()

  testLogging {
    exceptionFormat = 'full'
  }
}

test {
  useJUnitPlatform()
  failFast = true
}

task functional(type: Test) {
  description = "Runs functional tests"
  group = "Verification"
  testClassesDirs = sourceSets.functionalTest.output.classesDirs
  classpath = sourceSets.functionalTest.runtimeClasspath
}

task integration(type: Test) {
  description = "Runs integration tests"
  group = "Verification"
  testClassesDirs = sourceSets.integrationTest.output.classesDirs
  classpath = sourceSets.integrationTest.runtimeClasspath
  failFast = true
}

task smoke(type: Test) {
  description = "Runs Smoke Tests"
  testClassesDirs = sourceSets.smokeTest.output.classesDirs
  classpath = sourceSets.smokeTest.runtimeClasspath
}

task fortifyScan(type: JavaExec, description: 'Run fortify scan.')  {
  getMainClass().set("uk.gov.hmcts.fortifyclient.FortifyClientMainApp")
  classpath += sourceSets.test.runtimeClasspath
  jvmArgs = ['--add-opens=java.base/java.lang.reflect=ALL-UNNAMED']
}

processIntegrationTestResources {
  duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

processFunctionalTestResources {
  duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

checkstyle {
  maxWarnings = 100000
  toolVersion = '8.40'
  getConfigDirectory().set(new File(rootDir, 'config/checkstyle'))
}

pmd {
  toolVersion = "6.18.0"
  ignoreFailures = true
  sourceSets = [sourceSets.main, sourceSets.test, sourceSets.functionalTest, sourceSets.integrationTest, sourceSets.smokeTest]
  reportsDir = file("$project.buildDir/reports/pmd")
  // https://github.com/pmd/pmd/issues/876
  ruleSets = []
  ruleSetFiles = files("config/pmd/ruleset.xml")
}

jacoco {
  toolVersion = "0.8.10"
}

jacocoTestReport {
  executionData(test, integration)
  reports {
    xml.required.set(true)
    csv.required.set(false)
    xml.outputLocation.set(layout.buildDirectory.file("reports/jacoco/test/jacocoTestReport.xml"))
  }
}
project.tasks['sonarqube'].dependsOn test, integration, jacocoTestReport

sonarqube {
  properties {
    property "sonar.projectName", "IA :: Immigration & Asylum Home Office Mock API"
    property "sonar.projectKey", "IAHOMEOFFICEMOCKAPI"
    property "sonar.coverage.jacoco.xmlReportPaths", layout.buildDirectory.file("reports/jacoco/test/jacocoTestReport.xml").get().asFile
    property "sonar.exclusions", "src/main/java/uk/gov/hmcts/reform/iahomeofficemockapi/infrastructure/config/**," +
            "src/main/java/uk/gov/hmcts/reform/iahomeofficemockapi/generated/infrastructure/api/**," +
            "src/main/java/uk/gov/hmcts/reform/iahomeofficemockapi/generated/domain/entities/**," +
            "src/main/java/org/openapitools/configuration/**," +
            "src/main/java/uk/gov/hmcts/reform/iahomeofficemockapi/**"
  }
}

// before committing a change, make sure task still works
dependencyUpdates {
  def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { qualifier -> version.toUpperCase().contains(qualifier) }
    def regex = /^[0-9,.v-]+$/
    return !stableKeyword && !(version ==~ regex)
  }
  rejectVersionIf { selection -> // <---- notice how the closure argument is named
    return isNonStable(selection.candidate.version) && !isNonStable(selection.currentVersion)
  }
}

// https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
dependencyCheck {
  // Specifies if the build should be failed if a CVSS score above a specified level is identified.
  // range of 0-10 fails the build, anything greater and it doesn't fail the build
  failBuildOnCVSS = System.getProperty('dependencyCheck.failBuild') == 'true' ? 0 : 11
  suppressionFile = 'config/owasp/suppressions.xml'
  configurations.collect { it.name }.findAll {
    it.startsWith('org.openapitools.configuration') ||  it.startsWith('uk.gov.hmcts.reform.iahomeofficemockapi.generated')}

  analyzers {
    // Disable scanning of .NET related binaries
    assemblyEnabled = false
  }
}

dependencyManagement {
  dependencies {
    dependencySet(group: 'org.yaml', version: '2.0') {
      entry 'snakeyaml'
    }
    dependencySet(group: 'com.google.guava', version: '32.0.1-jre') {
      entry 'guava'
    }
    dependencySet(group: 'org.apache.tomcat.embed', version: '9.0.84') {
      entry 'tomcat-embed-core'
      entry 'tomcat-embed-el'
      entry 'tomcat-embed-websocket'
    }
    dependencySet(group: 'com.fasterxml.jackson.core', version: '2.16.0') {
      entry 'jackson-databind'
      entry 'jackson-core'
    }
  }
}

repositories {
  mavenLocal()
  mavenCentral()
  maven {
    url "https://dl.bintray.com/hmcts/hmcts-maven"
  }
  maven {
    url 'https://jitpack.io'
  }
}

def versions = [
  junit           : '5.8.2',
  junitPlatform   : '1.8.1',
  reformLogging   : '6.0.1',
  springBoot      : '2.7.18',
  serenity        : '3.2.3'
]

ext.libraries = [
  junit5: [
    "org.junit.jupiter:junit-jupiter-api:${versions.junit}",
    "org.junit.jupiter:junit-jupiter-engine:${versions.junit}",
    "org.junit.jupiter:junit-jupiter-params:${versions.junit}",
    "org.junit.platform:junit-platform-commons:${versions.junitPlatform}",
    "org.junit.platform:junit-platform-engine:${versions.junitPlatform}"
  ]
]

dependencies {
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-aop'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-json'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-validation', version: versions.springBoot

  implementation group: 'org.springdoc', name: 'springdoc-openapi-ui', version: '1.6.8'

  implementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.13'
  implementation group: 'ch.qos.logback', name: 'logback-core', version: '1.2.13'

  implementation group: 'com.github.hmcts.java-logging', name: 'logging', version: versions.reformLogging
  implementation group: 'com.github.hmcts.java-logging', name: 'logging-appinsights', version: versions.reformLogging
  implementation(group: 'org.springframework.cloud', name: 'spring-cloud-starter-netflix-hystrix', version: '2.2.10.RELEASE') {
    exclude group: "org.bouncycastle", module: "bcprov-jdk15on"
  }
  implementation(group: 'org.springframework.cloud', name: 'spring-cloud-starter-bootstrap' , version: '3.1.7') {
    exclude group: "org.bouncycastle", module: "bcprov-jdk15on"
  }
  implementation group: 'org.bouncycastle', name: 'bcprov-jdk18on', version: '1.74'
  implementation group: 'com.sun.xml.bind', name: 'jaxb-osgi', version: '2.3.3'
  // CVE-2020-9488
  implementation group: 'org.apache.logging.log4j', name: 'log4j-to-slf4j', version: '2.13.2'
  implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.13.2'

  implementation group: 'javax.xml.bind', name:'jaxb-api', version:'2.2.11'
  implementation group:'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: '2.12.0'
  implementation group: 'org.openapitools', name: 'jackson-databind-nullable', version: '0.1.0'
  implementation group: 'javax.validation', name: 'validation-api', version: '2.0.1.Final'

  implementation group: 'org.webjars.bower', name: 'jquery', version: '3.6.0'

  implementation group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.13'

  implementation group: 'org.apache.sling', name: 'org.apache.sling.api', version: '2.25.4'

  implementation group: 'com.nimbusds', name: 'nimbus-jose-jwt', version: '9.37.3'

  implementation group: 'net.minidev', name: 'json-smart', version: '2.5.0'

  testImplementation group: 'com.github.hmcts', name: 'fortify-client', version: '1.3.0'

  testImplementation libraries.junit5
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-test', {
    exclude group: 'junit', module: 'junit'
    exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
  }

  testImplementation(group: 'org.springframework.cloud', name: 'spring-cloud-contract-wiremock', version: '3.1.6') {
    exclude group: "com.github.tomakehurst", module: "wiremock-standalone"
  }
  testImplementation group: 'org.apiguardian', name: 'apiguardian-api', version: '1.1.2'

  functionalTestImplementation group: 'org.apiguardian', name: 'apiguardian-api', version: '1.1.2'
  functionalTestImplementation group: 'net.serenity-bdd', name: 'serenity-rest-assured', version: versions.serenity
  functionalTestImplementation group: 'net.serenity-bdd', name: 'serenity-core', version: versions.serenity
  functionalTestImplementation group: 'net.serenity-bdd', name: 'serenity-junit', version: versions.serenity
  functionalTestImplementation group: 'net.serenity-bdd', name: 'serenity-spring', version: versions.serenity
  functionalTestImplementation group: 'io.rest-assured', name: 'rest-assured-all', version: '4.5.1'
  functionalTestImplementation group: 'io.rest-assured', name: 'rest-assured', version: '4.5.1'

  smokeTestImplementation group: 'org.apiguardian', name: 'apiguardian-api', version: '1.1.2'
  smokeTestImplementation group: 'io.rest-assured', name: 'rest-assured', version: '4.5.1'

  integrationTestImplementation sourceSets.main.runtimeClasspath
  integrationTestImplementation sourceSets.test.runtimeClasspath

  functionalTestImplementation sourceSets.main.runtimeClasspath

  smokeTestImplementation sourceSets.main.runtimeClasspath
}

mainClassName = 'uk.gov.hmcts.reform.iahomeofficemockapi.generated.infrastructure.api.invoker.OpenAPI2SpringBoot'
def jarName = 'ia-home-office-mock-api.jar'

bootJar {
  getArchiveFileName().set(provider {
    'ia-home-office-mock-api.jar'
  })

  manifest {
    attributes('Implementation-Version': project.version.toString())
  }
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}

